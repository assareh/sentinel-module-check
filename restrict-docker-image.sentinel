# This policy uses the tfconfig/v2 import to require that all non-root
# ecsfargate-service modules provision docker images from the
# nginxdemos repository

# Import the tfconfig/v2 import, but use the alias "tfconfig"
import "tfconfig/v2" as tfconfig
# Standard strings import
import "strings"

### Parameters ###
# The address of the TFC or TFE server
param address default "app.terraform.io"
# The organization on the TFC or TFE server
param organization default "hashidemos"
# The module in question
param module default "ecsfargate-service/aws"
# The docker account allowed
param repository default "nginxdemos"

# Find instances of ecsfargate-service module
foundMCs = filter tfconfig.module_calls as index, mc {
  mc.module_address is "" and
  mc.source is (address + "/" + organization + "/" + module)
}

# Check docker_image in those
violatingMCs = filter foundMCs as index, mc {
  not strings.has_prefix(mc.config.docker_image.constant_value, repository)
}

# Print violation messages for invalid modules
if length(violatingMCs) > 0 {
  print("All Docker images must come from the", repository, "container registry.")
  for violatingMCs as address, mc {
    print("The module", mc.name, "has argument docker_image = ",
          mc.config.docker_image.constant_value)
  }
}

# Main rule
validated = length(violatingMCs) is 0
main = rule {
 validated is true
}
